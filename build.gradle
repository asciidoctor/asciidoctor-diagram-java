buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
        mavenCentral()
    }

    dependencies {
        classpath("net.researchgate:gradle-release:3.0.2")
    }
}

apply plugin: 'net.researchgate.release'

subprojects {
    apply plugin: 'java'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    group = 'org.asciidoctor'

    dependencies {
        implementation fileTree('lib')
        testImplementation(platform("org.junit:junit-bom:5.10.1"))
        testImplementation("org.junit.jupiter:junit-jupiter")
    }

    compileJava {
        sourceCompatibility = '17'
        targetCompatibility = '17'
    }

    task zip(type: Zip) {
        from jar
        from configurations.runtimeClasspath.allArtifacts.files
        from configurations.runtimeClasspath
    }

    task testJar(type: Jar, dependsOn: testClasses, group: 'Build') {
        archiveClassifier = 'tests'
        from sourceSets.test.output
    }

    test {
        useJUnitPlatform()
    }

    configurations {
        tests
    }

    artifacts {
        tests testJar
    }
}
